plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id "org.openapi.generator" version "5.3.0"
    id 'maven-publish'
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 27
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    openApiGenerate {
        generatorName = "kotlin"
        inputSpec = "$rootDir/swagger.json".toString()
        outputDir = "$buildDir/generated".toString()
        apiPackage = "com.immutable.sdk.api"
        invokerPackage = "com.immutable.sdk.invoker"
        modelPackage = "com.immutable.sdk.model"
        configOptions = [
                dateLibrary: "java8"
        ]
    }

    sourceSets {
        main {
            java.srcDirs = ["$buildDir/generated/src/main/kotlin"]
        }
    }

    // Generate all apis before every build
    preBuild.dependsOn(tasks.openApiGenerate)
}

dependencies {
    testImplementation 'junit:junit:4.+'

    // Required for the generated SDK code
    implementation "com.squareup.moshi:moshi-kotlin:1.13.0"
    implementation "com.squareup.okhttp3:okhttp:4.9.3"
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

def getVersionName = { ->
    return "0.0.7"
}

def getArtifactId = { ->
    return "imx-android-sdk"
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId 'com.immutable.sdk' // Replace with group ID
            artifactId getArtifactId()
            version getVersionName()
            artifact("$buildDir/outputs/aar/${getArtifactId()}-release.aar")
        }
    }

    repositories {
        maven {
            name = "imx-android-sdk"
            url = uri("https://maven.pkg.github.com/immutable/imx-android-sdk")
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}